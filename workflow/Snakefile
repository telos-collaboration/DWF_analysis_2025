configfile: "config/config.yaml"


from functools import reduce
from glob import glob
from operator import iand

import pandas as pd


metadata = pd.read_csv(config["metadata"])


CSVs = expand(
    "data_assets/{name}.csv",
    name=["WF_measurements", "plateau_fits_results", "eft_results"],
)
plots = expand(
    f"assets/plots/{{name}}{config['plot_filetype']}",
    name=[
        "chiral_aoverw0_vs_mPS",
        "fit_w0_mPS_mV",
        "Ls_scan_beta6.8",
        "Ls_scan_beta7.0",
        "mres_scan_largeLs",
        "mres_scan_mF0.02",
        "mres_scan_mF0.06",
        "effmass_ZA_B1M1",
        "effmass_mres_B4M8",
        "Ls_scan_beta6.9_mF0.06",
        "Ls_scan_beta6.9_mF0.04",
        "Ls_scan_beta7.2_mF0.08",
        "Ls_scan_beta7.2_mF0.10",
        "finitevolume_scan_beta6.9_mF0.06",
        "finitevolume_scan_beta6.9_mF0.04",
        "finitevolume_scan_beta7.2_mF0.04",
        "finitevolume_scan_beta7.2_mF0.06",
        "eft_result",
    ]
    + expand(
        "{prefix}_{beta}",
        prefix=[
            "GMOR_w0m0_vs_w0m_PS",
            "GMOR_w0m0_vs_w0fpi",
            "GMOR_w0m0_vs_w0_m_PS_fpi",
            "m0_vs_m_V_m_PS",
            "NLO_mPS_w0_beta",
        ],
        beta=set(metadata["beta"]),
    ),
)


rule all:
    input:
        "assets/info.json",
        "data_assets/info.json",
    default_target: True


def log_collator(wildcards, filters, template, filenames):
    return [
        f"{template}/{filename}".format(**datum)
        for filename in filenames
        for datum in metadata.to_dict(orient="records")
        if reduce(
            lambda x, y: x and y, (datum[name] == value for name, value in filters)
        )
    ]


include: "rules/provenance.smk"
include: "rules/wilson.smk"
include: "rules/raw_analysis.smk"
include: "rules/tables.smk"
include: "rules/main_results.smk"
include: "rules/param_scans.smk"
include: "rules/timing.smk"
include: "rules/flow_plots.smk"
